###################################################################
##This script contains the target labels from stream comments


label comment3Choice():
    $ macroChoice = True #If this is a decision happening in the macrogame, change this boolean so that the choice screen will appear in its default state
    $ comment3.click = False #makes the chat unclickable once it has been clicked.
    #This should reference the specific ChatEntry object that directs here through its target attribute.
    # eg. if comment64 directs to a certain label, add $ comment64.click = False, to turn it off
    $ narrator = reg_narrator # you also need to manually change the narrator so that the text appears outside the micro game
    menu:
        "Do I really wanna bring the mood down with how tough my test was?"
        "Kinda rough":
            player "Oh man, I had this test earlier in the week. Spent all Monday and Tuesday cramming."
            player "Fingers crossed I passed, essay tests are not my forte."
            $ pdEngagement -= 1
            $ csEngagement += 1
        "It was fine":
            player "Nothing much happened actually. School is what it is. Glad to be back on the game now!"
    $ macroChoice = False #set macroChoice back to false at the end of this interaction so that the 
    #choice screen will appear appropriately for micro-game choices.
    if macroNarration == False:
        $ narrator = alt_narrator
    else:
        $ narrator = reg_narrator
    return

#Comments after Decision 1
label streamAsksRomance():
    $ macroChoice = True
    $ kkAsksRomance.click = False
    $ narrator = reg_narrator
    menu:
        "I'm thinking Jennica":
            player "Jennica's sweet, I'm leaning her at the moment!"
            $ csEngagement += 1
            $ romanceJennica = True
            $ romanceTeresa = False
            $ romanceAma = False
            pause 0.5
            $ chatter_list.append(csJennica)
            pause 1.0
        "Probably Teresa":
            player "Teresa for sure. She's got an edge I appreciate."
            $ kkEngagement += 1
            $ romanceTeresa = True
            $ romanceAma = False
            $ romanceJennica = False
            pause 0.5
            $ chatter_list.append(kkTeresa)
            pause 1.0
        "Ama all day, for sure":
            $ pdEngagement += 1
            $ kkEngagement += 1
            $ csEngagement += 1
            player "Actually, I'm holding out that there will be an Ama romance path."
            $ romanceAma = True
            $ romanceTeresa = False
            $ romanceJennica = False
            pause 1.0
            $ chatter_list.append(commentNothingPD)
            pause 1.0
            $ chatter_list.append(commentNothingKK2)
            pause 1.0
    $ macroChoice = False
    if macroNarration == False:
        $ narrator = alt_narrator
    else:
        $ narrator = reg_narrator    
    return

##Comments after Decision 2
label CoriolisTalksDnDOutlaw():
    $ macroChoice = True
    $ csOutlawD2.click = False
    $ narrator = reg_narrator
    menu:
        "I've never played DnD before":
            $ DnDPlayer = False
            player "I've actually never played DnD before."
            player "A friend of mine tried to get a group together, but people kept bailing so she lost momentum on it."
            $ chatter_list.append(csDnDvg2)
            pause 0.3
            $ chatter_list.append(kkDnDvg2)
            pause 0.6
            $ chatter_list.append(pdDnDvg2)
        "Right!?":
            $ DnDPlayer = True
            player "For real, stealth never goes the way you want it to."
            $ chatter_list.append(kkDnDvg2play)
            pause 0.6
            $ chatter_list.append(pdDnDvg2)
            pause 1.0
    $ macroChoice = False
    if macroNarration == False:
        $ narrator = alt_narrator
    else:
        $ narrator = reg_narrator   
    return

label CoriolisTalksDnDMarshal():
    $ macroChoice = True
    $ csMarshalD2.click = False
    $ narrator = reg_narrator
    menu:
        "I've never played DnD before":
            $ DnDPlayer = False
            player "I've actually never played DnD before."
            player "A friend of mine tried to get a group together, but people kept bailing so she lost momentum on it."
            $ chatter_list.append(csDnDvg2)
            pause 0.3
            $ chatter_list.append(kkDnDvg2)
            pause 0.6
            $ chatter_list.append(pdDnDvg2)
        "You gotta have support":
            $ DnDPlayer = True
            player "Support classes are so underrated in DnD."
            $ chatter_list.append(kkDnDvg2play)
            pause 0.6
            $ chatter_list.append(pdDnDvg2)
            pause 1.0
    $ macroChoice = False
    if macroNarration == False:
        $ narrator = alt_narrator
    else:
        $ narrator = reg_narrator   
    return

label DnDClassTalk():
    $ macroChoice = True
    $ kkDnDvg2.click = False
    $ narrator = reg_narrator
    $ csEngagement += 1
    menu: #could log this choice for future reference if you want
        "Barbarians seem fun":
            player "Barbarians seem super fun. See thing smash thing hahaha"
        "Something support-focused.":
            $ csEngagement += 1
            player "I like helping my party in multiplayer games, so I was looking into bards when we were trying to plan the game."
            $ newChat = csDnD2vg2
            $ chatter_list.append(csDnD2vg2)
            pause 1.0
        "Paladins might be cool":
            $ pdEngagement += 1
            player "I do like the idea of Paladins actually. Bit of magic, bit of weapons."
            $ chatter_list.append(pdDnD2vg2)
            pause 1.0
    $ macroChoice = False
    if macroNarration == False:
        $ narrator = alt_narrator
    else:
        $ narrator = reg_narrator   
    return

label DnDClassTalkplay():
    $ macroChoice = True
    $ kkDnDvg2play.click = False
    $ narrator = reg_narrator
    $ csEngagement += 1
    menu:
        "I like Rangers":
            player "Rangers are fun, I like their their flexibility."
        "Barbarians":
            player "Barbarians are super fun. See thing smash thing hahaha"
        "Spellcasters":
            player "Anything with spellcasting. Wizards, bards, gimme some fun magic."
            $ chatter_list.append(csDnD2vg2)
            pause 1.0
        "Paladins!":
            $ pdEngagement += 1
            player "PickledDragons you nailed it! I love the role play aspect of paladins."
            $ chatter_list.append(pdDnD2vg2)
            pause 1.0
    $ macroChoice = False
    if macroNarration == False:
        $ narrator = alt_narrator
    else:
        $ narrator = reg_narrator   
    return
