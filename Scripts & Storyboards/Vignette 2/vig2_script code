# The script of the game goes in this file.

# Declare characters used by this game. The color argument colorizes the
# name of the character.

# The script of the game goes in this file.

# Declare characters used by this game. The color argument colorizes the
# name of the character.

#narrator definitions
default reg_narrator = Character(None, window_style = "window")
default alt_narrator = Character(None, window_style = "ig_window", what_style = "ig_dialogue", what_color = "#000000", window_background="images/textbox/ig_textbox_blue.png")

define player = Character("[my_name]") #represents the protagonist's name which will be defined by the player via an input screen.
define mod = Character("Jessie")
define bro = Character("El")
define player_nvl = Character("[my_name]", kind = nvl, image = "captain_stream")
define mod_nvl = Character("Jessie", kind = nvl, image = "profile1small")
define bro_nvl = Character("El", kind = nvl )
define adv_menu = menu

#Justin: this is how I define the micro game characters in their stream and non-stream versions
#As you can see the style is different to be more compatible with the asset I made 
define m = Character("Moze", window_style = "window", who_color="#a333ff", what_color="#000000", image = "captain", window_background = "images/textbox/textbox_purple.png")
define mS = Character ("Moze", window_style = "ig_character", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#a333ff", what_color = "#000000", image = "captain_stream", window_background="images/textbox/ig_textbox_purple.png")

define en = Character("Teresa", window_style = "window", who_color="#FF3333", what_color="#000000", image = "engineer", window_background = "images/textbox/textbox_red.png")
define enS = Character ("Teresa", window_style = "ig_character", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color="#FF3333", what_color="#000000", image = "engineer_stream", window_background="images/textbox/ig_textbox_red.png")

define p = Character("Jennica", window_style = "window", who_color="#ef8f3a", what_color="#000000", image = "pilot", window_background = "images/textbox/textbox_orange.png")
define pS = Character ("Jennica", window_style = "ig_character", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color="#ef8f3a", what_color="#000000", image = "pilot_stream.png", window_background = "images/textbox/ig_textbox_orange.png")

define mac = Character("MAC", window_style = "window", who_color="#33a3ff", what_color="#000000", image = "mac", window_background = "images/textbox/textbox_blue.png")
define macS = Character("MAC", window_style = "ig_character", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#33a3ff", what_color = "#000000", image = "mac_stream", window_background="images/textbox/ig_textbox_blue.png")


define shipcom = Character("Ship Intercom", window_style = "ig_window", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#000000", what_color = "#000000", window_background="images/textbox/ig_textbox_blue.png")
define agent1 = Character("Customs Agent", window_style = "ig_window", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#000000", what_color = "#000000", window_background="images/textbox/ig_textbox_blue.png")
define smatt = Character("Matticus", window_style = "ig_window", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#000000", what_color = "#000000", image = "matticus_stream", window_background="images/textbox/ig_textbox_blue.png")
define goon =  Character("Reginald", window_style = "ig_window", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#000000", what_color = "#000000", image = "goon_stream", window_background="images/textbox/ig_textbox_blue.png")
define worker = Character("Otacon", window_style = "ig_window", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#000000", what_color = "#000000", image = "worker_stream", window_background="images/textbox/ig_textbox_blue.png")
define mattdoorbell = Character("Doorcom", window_style = "ig_window", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#000000", what_color = "#000000", window_background="images/textbox/ig_textbox_blue.png")
define hsguard1 = Character("Guard", window_style = "ig_window", what_style = "ig_dial", namebox_xpos = 250, namebox_ypos = 50, who_color = "#000000", what_color = "#000000", window_background="images/textbox/ig_textbox_blue.png")

#Additional variables
default viewCount = 8 #viewCount changes how many viewers are displayed in the streamdetails screen
default macroChoice = False  #this variable adjusts where the choice screen will appear. It should be False when decisions happen in microgame. It should be True when decisions happen in macro game.
default macroNarration = True #this variable helps determine if a macro game choice should return us to narration style that is micro game or macro game

#variables to track morality
default marshal = 2
default outlaw = 3

#variables to track interest of player
default reluctance = 0
default enthusiasm = 0
default energy = 5

#variables to track attributes
default curiosity = False

#variables to track viewer engagement
default csEngagement = 5
default pdEngagement = 3
default kkEngagement = 4

#Approval of micro-game characters
default teresaApproval = 3
default jennicaApproval = 4
default shnzi = True

init: 
    $ stream_left = Position (xpos=0.13, ypos=0.6) #stream view
    $ stream_right = Position (xpos = 0.67, ypos=0.6)
    $ stream_center = Position (xpos=0.40, ypos=0.6)
    $ screen_left = Position (xpos = 0.2, ypos = 0.7) #non-stream view
    $ screen_right = Position (xpos = 0.8, ypos = 0.7)

label start:
    #these six lines define transforms that can be used to align microgame characters where we want them on the screen
    #They're based on two images (profile1small and profile2small) and will need to be changed once we know the dimensions of character portraits
    #We'll want all our character portraits to be the same size so this method of defining transforms works consistently
    transform gameleft:
        xpos 0
        ypos 615
    transform gameright:
        xpos 1249
        ypos 615


    "Hello! This is the start of the game!"

    "So you should make a new name!"
    $ my_name = renpy.input("Your name is: ", length = 16) #invokes an input screen that allows player to define their own name. Length determines how long the name can be.

    "Your name is now [player]" #the player's name can now be interpellated using this method

    $ narrator = reg_narrator

    "This is a prototype to test the integration of stream comments with the in-game script."
    "Comments in the chat that are highlighted in yellow can be clicked on to interact with."
    "We will start now."
    jump gameStart

label gameStart():
    show streamview
    show screen streamDetails
    show screen streamChat
    $ comments_list.append(comment1)
    $ comments_list.append(comment2)
    $ comments_list.append(comment4)

    "You begin the stream and see some of your viewers commenting in the chat."
    "You take a second to check in with the viewers."
    "And then boot up the game."
    "You load the save file you were previously playing, and are ready to go."
    jump vig2Start

###SCENE 1###
label vig2Start(): 
    scene ship_hallway_stream at topleft with dissolve
    show streamview at center onlayer master

    $ macroNarration = False
    $ narrator = alt_narrator
    $ chatter_list.append(vig2_sc1_comment1)
    $ viewCount += 1
    "The \"Approaching Planet\" notification alarm jolts me out of bed."
    $ chatter_list.append(vig2_sc1_comment2)
    shipcom "Alert! Approaching Planet Gibian V. All hands on deck."
    show captain_stream at stream_left
    mS "Ugh..."
    "I'd be annoyed if I was actually sleeping."
    "I haven't been able to get any real sleep since Anatar forced my hand."
    "What did he think I was gonna do? Just let him go?"
    shipcom "Alert! Approaching Planet Gibian V. All hands on deck."
    "Right. Maybe it's a blessing that I don't have so much time to reflect."
    $ chatter_list.append(vig2_sc1_comment3)
    "Better get down there."
    hide captain_stream with Dissolve (0.4)

    jump Vig2BridgeScene

label Vig2BridgeScene():
    scene bridge_stream at center with dissolve
    show streamview at center onlayer master
    show pilot_stream at stream_left
    show engineer_stream at stream_right
    show mac_stream at stream_center
    #repeat for additional characters
    "As I step onto the bridge, Jennica, Teresa and Mac are discussing the visualization on the ship's computer."
    $ chatter_list.append(vig2_sc1_comment4)
    "The visualization shows Gibian V, a backwater frontier planet."
    "A backwater with an old associate running it."
    mS "We ready to land?"
    $ chatter_list.append(vig2_sc1_comment5)
    pS "Can I reiterate again that this is a bad idea?"
    "Teresa rolls her eyes."
    $ chatter_list.append(vig2_sc1_comment6)
    enS "Here we go again."
    $ chatter_list.append(vig2_sc1_comment7)
    pS "Look, I know we need to cover our trail from Ama."
    $ chatter_list.append(vig2_sc1_comment8)
    pS "But is Matticus really the one we want to do it? The guy was a skeeve when we knew him."
    macS "Skeeve. Noun. An immoral or repulsive person."
    enS "What other options do we have? Who else is gonna help us?"
    $ chatter_list.append(vig2_sc1_comment9)
    pS "I don't know, I just think-"
    enS "Let me just pull a \"forger with a heart of gold and access to bigcorp databases\" out of my purse and we'll be set!"
    pS "That doesn't mean we should trust Matticus, the wannabe godfather of a one-horse po-dunk  planet!"
    $ chatter_list.append(vig2_sc1_comment10)
    "Jennica's not wrong. Matticus was always particularly unscrupulous, even amongst the Snake-Hawks."
    "But Teresa's also got a point, we aren't exactly spoilt for choice."
    $ chatter_list.append(vig2_sc1_comment11)
    "Better the devil you know, eh?"
    mS "I understand the concern. But we don't really have another option."
    mS "We don't have to trust him, I certainly don't."
    $ chatter_list.append(vig2_sc1_comment12)
    mS "But we can't afford to be picky with our contacts."
    "A look of begrudging acceptance flashes across Jennica's face."
    pS "Ok. So what's the plan?"
    pause 1.0
    "Teresa inputs a set of coordinates into the ship computer."
    $ chatter_list.append(vig2_sc1_comment13)
    "The visualization zooms in on the capital of Gibian V, Montserrat."
    "A map of the city appears on the computer, along with images of the spaceport, customs depot, and Matticus's compound."
    enS "One benefit of Gibian being such a middle-of-nowhere planet is that we won't have to go far to find our guy."
    enS "Matticus became the magistrate and Bigcorp security rep for this sector a couple of years back."
    enS "He's never really been one for subtleties."
    "The visualization zooms in on images of Matticus' compound."
    "It's enormous, by far the largest building in town, complete with ostentatious fences emblazoned with his initials."
    enS "Case in point."
    pS "Charming."
    macS "Who is this \"Skeeve?\""
    enS "Savlian Matticus. He was in our old crew."
    pS "He certainly landed on his feet since we disbanded."
    enS "Yeah, he's been remarkably successful for a magistrate of a tiny frontier planet."
    macS "He is abusing his authority for personal gain?"
    mS "No, kidding." 
    pS "Alright, so it'll be easy to find him. What's the deal with the customs depot?"
    enS "Seems typical for a small planet. Standard issue scanners from half a decade ago."
    enS "Shouldn't be too much of a problem to fool them."
    mS "What about Ama? Think she might have eyes there?"
    enS "On Gibian V?"
    mS "Fair point. What about MAC? Do you think they might be on the lookout for him?"
    mS "They did put out an APB."
    enS "I doubt it. Takes a while for those bulletins to make it out here."
    enS "I'm not convinced they even do."
    pS "All the same, we're going to have to take him along."
    pS "We can't risk them finding him here in a surprise scan of the ship while we're away."
    $ chatter_list.append(vig2_sc1_comment14)
    mS "That's going to have to be good enough. Are we all good with this plan?"
    "Teresa nods. Jennica looks vexed, but eventually nods as well." 
    mS "Alright let's get moving."
    pS "Aye Aye."
    "Jennica begins the ship's descent to the spaceport."

    jump GibianVCustomsDepot

###SCENE 2###
label GibianVCustomsDepot(): 
    scene port_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    "The depot bustles with loud and chaotic life."
    "The tiny barebones facility seems like it's on the verge of bursting."
    "There are barely a dozen agents staffing the whole building."
    "New arrivals pile in hurriedly, while departures crowd at the checkpoint leading to the spaceport."
    "The air is thick and humid with the sheer amount of people here."
    show engineer_stream at stream_right with dissolve
    "Teresa sighs as we arrive at the main depot."
    enS "I hate customs."
    $ chatter_list.append(vig2_sc2_comment1)
    "Teresa's eyes dart around the depot facilities."
    show pilot_stream at stream_left with dissolve
    pS "Well getting a permit to fly our ship around here would attract a lot more attention."
    pS "Not to mention, would require an inspection of all cargo."
    "Jennica gestures to the small crate holding MAC."
    pS "Which is a nonstarter." 
    "Teresa rolls her eyes as we get in the line marked \"New Arrivals.\""
    $ chatter_list.append(vig2_sc2_comment2)
    "Those same eyes grow wide and panicked."
    enS "Shit! We have a problem."
    mS "I knew this was too easy. What is it?"
    "Teresa gestures towards a customs official, scanning cargo crates."
    enS "That's a bigcorp scanner. I imagine it'll be able to detect their stolen property."
    pS "So APB's do make it out here..."
    mS "We don't have time to bicker."
    mS "We need a plan to get through without them finding MAC."
    enS "Agreed."
    "Teresa pulls out a metal sphere with numerous access ports."
    enS "I think I could whip up a diversion with this if you buy me a little time."
    $ chatter_list.append(vig2_sc2_comment3)
    pS "One of {i}your{/i} diversions? Here?"
    "Jennica appears irritated by this idea."
    pS "With all these people? It's going to cause a panic and stampede!"
    $ chatter_list.append(vig2_sc2_comment4)
    enS "You have a better plan?"
    pS "Yeah. We may be overthinking this."
    $ chatter_list.append(vig2_sc2_comment5)
    pS "These customs guys can't exactly be flush with cash, especially since they're working for Matticus."
    $ chatter_list.append(vig2_sc2_comment6)
    pS "A little grease in the right palm should do the trick."
    "Teresa scoffs."
    enS "Sure, unless one of them happens to have ambitions of a promotion?"
    enS "An employee-of-the-month type will report us to bigcorp in a heartbeat!"
    $ chatter_list.append(vig2_sc2_comment7)
    pS "As opposed to causing a stampede and killing people?"
    pS "A whole bunch of dead innocent bystanders isn't exactly clandestine."
    "The crew looks to me."
    enS "Your call captain. What do you think?"
    "Jennica has a point. There's a ton of innocent people here, it's very likely someone will get hurt if we cause a panic."
    "Teresa's is also right though, if one of these officials reports us to bigcorp then we might be looking at the inside of the jail cell quick."
    menu: #decision2
        "Should Teresa prepare a distraction or will you look for an official to bribe?"
        "Have Teresa prep the distraction":
            $ outlaw += 1
            $ pdEngagement += 1
            $ kkEngagement += 1
            jump GibianVCustomsOutlaw
        "Follow Jennica and bribe a guard":
            $ marshal += 1
            $ pdEngagement -= 1
            $ csEngagement += 1
            $ kkEngagement -= 1
            jump GibianVCustomsMarshal

label GibianVCustomsMarshal():
    scene port_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show engineer_stream at stream_right with dissolve
    show pilot_stream at stream_left with dissolve
    "Jennica's right. A diversion could hurt a lot of people."
    "And it could definitely attract the wrong attention."
    $ chatter_list.append(vig2_sc2_mar_comment1)
    mS "We can't cause a panic here. We're going with Jennica's plan."
    "Teresa reluctantly puts away her device."
    $ chatter_list.append(vig2_sc2_mar_comment2)
    enS "Fine. I hope we don't regret this."
    pS "Good. Let's see if we can find an agent who looks like they could use a break."
    mS "Right, let's do a quick scout."
    $ chatter_list.append(vig2_sc2_mar_comment3)
    "There's a few different agents spread across the depot."
    "Most look pretty... ordinary."
    "Teresa gestures towards a younger looking agent with glasses."
    "His uniform is spotless, perfectly tucked and pleated with meticulous precision."
    enS "That one looks pretty eager, definitely not him." 
    "Jennica zeros in on an agent at the \"Hazardous Luggage\" counter, subtly gesturing to me to look at him."
    "His uniform is wrinkled, shirt half tucked in a rushed panic."
    "The man looks deflated, with dark bags under his eyes."
    $ chatter_list.append(vig2_sc2_mar_comment4)
    "He looks like he may collapse at any moment."
    pS "That's our mark."
    mS "Good find. He looks like the dictionary page for \"disheartened.\""
    $ chatter_list.append(vig2_sc2_mar_comment5)
    "Jennica chuckles."
    hide pilot_stream with Dissolve(0.4)
    "Teresa nods to us. Let's do this."
    hide engineer_stream with Dissolve(0.4)
    show customs_stream at stream_center with Dissolve(0.6)
    "As I approach the customs officer, his eyes perk up."
    $ chatter_list.append(vig2_sc2_mar_comment6)
    agent1 "Hello Ma'am. How might I assist you today?"
    mS "You strike me as a hard worker sir." 
    agent1 "Excuse me?"
    "He seems a little confused."
    $ chatter_list.append(vig2_sc2_mar_comment7)
    mS "It seems like this whole place is propped up by you eh?"
    "His eyes relax as he begins to get what I'm saying."
    agent1 "You don't know the half of it." 
    agent1 "I've been working back to back shifts this whole week!" 
    mS "Really?! That doesn't sit right with me."
    mS "Not one bit."
    "A smirk begins to spread across his face."
    agent1 "I do appreciate that."
    mS "I wouldn't be able to sleep if I didn't make it right."
    "I extend my hand to shake his, slipping him a 100 credit note."
    "He quickly slips it into his pocket before looking around the depot at his fellow employees."
    $ chatter_list.append(vig2_sc2_mar_comment8)
    agent1 "Wait by the breakroom for me."
    "I see his eyes are pointing at the staff breakroom on the other side of the depot."
    agent1 "Five minutes."
    "All of a sudden, he raises his voice to be easily audible by nearby bystanders and co-workers."
    agent1 "I'm terribly sorry but I can't help you Maam, you need to enter customs regularly."
    "He motions for me to move on."
    hide customs_stream with dissolve
    "I signal the direction of the breakroom to Teresa and Jennica as I begin strolling towards it."
    show engineer_stream at stream_right with Dissolve (0.4)
    show pilot_stream at stream_left with Dissolve(0.4)
    "Teresa looks worried."
    "A minute passes."
    $ chatter_list.append(vig2_sc2_mar_comment9)
    "And another."
    "Three minutes." 
    enS "You sure he's not ratting us out as we speak?"
    enS "If I have to break us out of the Gibian V penitentiary..."
    "A muffled voice perks up from the crate."
    macS "Penitentiary. Noun. A place for imprisoning crim-"
    enS "SHUSH!"
    "The door creaks ajar."
    show customs_stream at stream_center with Dissolve(0.2)
    "The customs official peeks his head through the door."
    agent1 "Quick. Stay close."
    "We don't need to be told twice."
    hide engineer_stream with Dissolve(0.3)
    hide pilot_stream with Dissolve(0.3)
    $ chatter_list.append(vig2_sc2_mar_comment10)
    "The agent whisks us from room to room, out from one backdoor to another."
    "Soon we're outside, next to the trash dump for the depot."
    #$ addStoryBeat()
    pause 1.0
    "We're out."
    agent1 "Right so..."
    agent1 "Y'all strike me as the types to not need reminding that we never met."
    pS "Mighty nice of ya to help us out!"
    "The agent wordlessly tips his hat before disappearing through the door."
    pS "Simple as it gets!"
    enS "Yes. Almost too much so..."

    jump matticusDoor

label GibianVCustomsOutlaw():
    scene port_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show engineer_stream at stream_right with Dissolve(0.4)
    show pilot_stream at stream_left with Dissolve(0.4)
    $ chatter_list.append(vig2_sc2_out_comment1)
    "Teresa's right. We can't afford a goody-two-shoes ratting us out."
    "We can't trust the officials here. We'll have to get through our own way."
    mS "Bribing's too risky. We're going with Teresa's plan."
    "Jennica sighs."
    pS "Alright, but we better make it quick. I don't want too many people to get hurt."
    enS "Hurt? Please! This will be the most exciting thing to happen to them in years!"
    mS "Maybe let's keep the excitement to a minimum?"
    $ chatter_list.append(vig2_sc2_out_comment2)
    "Teresa rolls her eyes at me."
    enS "I was only joking. Anyhow..."
    "Teresa begins surveying the depot."
    $ chatter_list.append(vig2_sc2_out_comment3)
    enS "Right. So, Jennica will slip this device into the customs declaration box."
    enS "Once she is clear, I will activate it."
    enS "Then we make a break for the exit as soon as the second phase begins."
    mS "What's the second phase?"
    $ chatter_list.append(vig2_sc2_out_comment4)
    enS "I'm glad you ask! It is the {i}conflagrant{/i} phase."
    "A muffled voice perks up from the crate."
    macS "Conflagrant. Adjective. Burning or bl-."
    enS "SHUSH!"
    $ chatter_list.append(vig2_sc2_out_comment5)
    pS "Seriously?"
    mS "We're trying {i}not{/i} to a attract attention Teresa."
    enS "Fires happen in these depots all the time!"
    enS "They'll blame it on some farmer using too much fertilizer or something."
    mS "Just keep it under control."
    "Teresa smirks slyly."
    enS "I always do."
    "Teresa pulls several parts from her bag and begins plugging them into her device."
    $ chatter_list.append(vig2_sc2_out_comment6)
    enS "Just a little tweak to the capacitors should do the trick!"
    enS "There!"
    $ chatter_list.append(vig2_sc2_out_comment7)
    "A spark flies off of the device as Teresa hands it to Jennica."
    pS "Sure this is safe? Looks like it's gonna turn my face into ash."
    enS "As safe as one of your daring escape maneuvers at the very least."
    "Jennica chuckles."
    pS "Good one."
    $ chatter_list.append(vig2_sc2_out_comment8)
    mS "We ready?"
    "Jennica and Teresa nod to me."
    "Teresa and I head towards the customs declarations line."
    "Jennica carefully walks by the customs declaration box and slips the device inside."
    "As soon as Jennica begins making her way back to us, Teresa presses a button on her wrist computer."
    enS "Let's show these rubes a good time."
    "A thin plume of white smoke begins to emanate from the box."
    "As the smoke grows thicker, security officials approach the box to investigate."
    $ chatter_list.append(vig2_sc2_out_comment9)
    "A commotion begins to spread across the depot as more and more of the crowd notices the smoke."
    enS "Get ready."
    "Once the security guards close in on the box, Teresa activates the device again."
    "A powerful blaze erupts from the box."
    "Black smoke begins to billow into the depot as the fire alarm starts blaring."
    pS "Let's haul ass."
    mS "You read my mind."
    "The crowd explodes into a panic. Hordes of bodies rush towards the exit."
    "It's easy enough to slip through the crowd unnoticed."
    $ chatter_list.append(vig2_sc2_out_comment10)
    "Whatever security was covering the exit is long gone by the time we reach the doors."
    "The doors burst open as the crowd pours out of the depot."
    $ chatter_list.append(vig2_sc2_out_comment11)
    mS "Let's get off the main street!"
    "Teresa and Jennica nod as I point towards a small alleyway."
    "We're out and on our way to Matticus's compound."
    jump matticusDoor

###SCENE 3###
label matticusDoor():
    scene compound_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    "It's only a few blocks to Matticus' compound."
    "Not like we could miss it though."
    show pilot_stream at stream_left with dissolve
    "Jennica's eyes widen as we first see the gates."
    pS "Holy smokes!"
    "The gates dwarf the rest of the block."
    "They're taller than the whole building across the street, and shining with gaudy golden accents and initials."
    $ chatter_list.append(vig2_sc3_comment1)
    show engineer_stream at stream_right with dissolve
    enS "Understated. How classy."
    mS "Yeah I need a second to just appreciate the elegance of it all."
    pS "Can we get this over with?"
    $ chatter_list.append(vig2_sc3_comment2)
    "Jennica motions to the intercom."
    pS "I'd like to spend as little time dealing with this slum-king as possible."
    "I press on the intercom button outside the gate."
    "A dial tone begins to play"
    mattdoorbell "Name and ID?"
    mS "We're old friends of Sav."
    $ chatter_list.append(vig2_sc3_comment3)
    mattdoorbell "Sure. And my wife loves me more than anything."
    mS "What?"
    mattdoorbell "Oh sorry, I thought we were practicing how to believe our own delusions."
    mattdoorbell "The boss doesn't see \"friends.\" He doesn't have em."
    $ chatter_list.append(vig2_sc3_comment4)
    mS "Real old. Before his coronation."
    mattdoorbell "Snake Hawks?"
    mS "I don't like to brag."
    mattdoorbell "Thought there weren't any left after your last job."
    mS "Yeah me too. Can we come in?"
    mattdoorbell "I need a little more proof than that."
    menu:
        "Bring up an old debt": #Lore
            mS "Remind him that he owes me his whole ass for saving him on Tiber III."
            mattdoorbell "Alright give me a sec."
            "The line goes quiet."
            $ chatter_list.append(vig2_sc3_lore_comment1)
            pS "Not sure if {i}that{/i} was the best story to bring up Cap."
            mS "I mean, there's no way he forgot about it."
            pS "That's my point..."
            "The intercom lights up again."
            $ chatter_list.append(vig2_sc3_lore_comment2)
            mattdoorbell "You're good. But not here."
            mattdoorbell "The boss has to be careful who he's seen with."
            mattdoorbell "He'll meet you in the warehouse down the road. Here's the coordinates."
            jump meetingMatticus
        "Threaten the doorman": #Threaten
            mS "Listen buddy. Just let us in."
            mS "Do you wanna explain to him that you pissed off his old merc crew?"
            mattdoorbell "No, I just-"
            "He's flustered."
            $ chatter_list.append(vig2_sc3_threat_comment1)
            mS "Tell him Moze is going to do to this gate what you did to that bar on Forsteuk if you don't let her in."
            mattdoorbell "Fine. Have it your way."
            "The line goes quiet."
            $ chatter_list.append(vig2_sc3_threat_comment2)
            enS "As much as it amuses me to see you coerce some mook..."
            enS "Was that really the best course of action?"
            mS "He's a big boy. He'll be fine."
            $ chatter_list.append(vig2_sc3_threat_comment3)
            mattdoorbell "You're good. But not here."
            mattdoorbell "The boss has to be careful who he's seen with."
            mattdoorbell "He'll meet you in the warehouse down the road. Here's the coordinates."
    jump meetingMatticus

###SCENE4###
label meetingMatticus():
    scene warehouse_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    $ chatter_list.append(vig2_sc4_comment1)
    "The dingy warehouse has a hint of stale air."
    "Just as I think to look how long we've been waiting, the doors swing open."
    show matticus_stream at stream_left with dissolve
    smatt "As I live and breathe!"
    "Matticus waddles through the doors into view, flanked on either side by henchmen."
    "He approaches me smiling, revealing rows of yellowed teeth."
    smatt "I thought my doorman was lying! But here you are."
    "Matticus pulls out a cigar and lights it."
    mS "Yeah. Here we are."
    smatt "What a delightful surprise. And you brought Jenn too."
    "Jennica silently nods, trying to redirect the smoke away from her face."
    smatt "Quite a reunion!"
    "Matticus takes a long drag on his cigar before looking at me expectantly. The fake smile fades from his face."
    smatt "...So? I assume you didn't come all this way to reminisce about the good ol' days."
    mS "No. We need a favor."
    smatt "Do you now?"
    mS "Bigcorp is looking for us. We want to make that difficult."
    smatt "I see. I wonder if it has to do with the break in to their top secret facility a few weeks back."
    "The smoke wafts out of Matticus' grin like fog."
    mS "It might."
    mS "And they hired our old boss to hunt us."
    "Matticus stops smirking."
    smatt "Oh. Interesting. She doesn't come cheap."
    smatt "You really kicked the hornet's nest eh?"
    "A voice perks up from the crate."
    macS "Kick the hornet's nest. Idiom. Provoking a situation whi-"
    show goon_stream at stream_right
    goon "What the hell!?"
    "Matticus' guards spring into action and draw their weapons."
    show mac_stream at stream_center with Dissolve(.5)
    "MAC sheepishly opens the crate lid and pokes his head out."
    smatt "Who's this?"
    hide mac_stream with dissolve
    mS "He's what was in that facility."
    show matticus_stream at stream_center with move
    "Matticus approaches MAC, looking him over."
    hide goon_stream
    smatt "Neat. So how much you figure it's worth?"
    $ chatter_list.append(vig2_sc4_comment2)
    mS "We're just looking to get him to the Fireflies."
    "Matticus' eyes widen. His voice takes on a singsong quality."
    smatt "Moze! You've gone soft!"
    smatt "I never figured you for a tenderhearted philanthropist!"
    $ chatter_list.append(vig2_sc4_comment3)
    show pilot_stream at stream_left with Dissolve(0.5)
    show engineer_stream at stream_right with Dissolve(0.5)
    "Jennica rolls her eyes."
    enS "Look will you help us or not?"
    "His face twists into a grin again."
    smatt "I might. First I need you to do something for me."
    mS "I'm listening."
    $ chatter_list.append(vig2_sc4_comment4)
    smatt "It's a simple job. Right in your wheelhouse."
    smatt "There's an off-world shipment coming in later today. It's going to have an abrupt reunion with gravity."
    smatt "I was gonna hire mercs, but you'll save me a few bucks."
    pS "What's in the shipment?"
    "Matticus continues to smirk as he takes another long drag off his cigar."
    smatt "It's not important."
    $ chatter_list.append(vig2_sc4_comment5)
    pS "I don't care for that answer one bit."
    smatt "You'll have to learn to like it, or you'll find out what Deadeye's got planned for you."
    enS "That's a reasonable point Captain."
    mS "What alternatives do we have Jenn?"
    "Jennica reluctantly agrees."
    $ chatter_list.append(vig2_sc4_comment6)
    mS "And if we do this?"
    smatt "I'll feed false reports into the Bigcorp security database."
    smatt "Deadeye'll be looking for you in every corner of the galaxy, except for where you actually are."
    smatt "And you bleeding hearts will be free to save the universe."
    enS "Not an unreasonable agreement."
    pS "I don't buy it. Something seems fishy."
    smatt "Oh Jenn. I'm wounded. After all we've been through?"
    $ chatter_list.append(vig2_sc4_comment7)
    smatt "You still don't trust me?"
    pS "Are you surprised?"
    smatt "I don't get surprised honey."
    pS "Seemed surprised at the door."
    "Matticus smirks at Jennica."
    $ chatter_list.append(vig2_sc4_comment8)
    smatt "Oh? Did I?"
    "Matticus looks at me impatiently."
    smatt "Don't waste my time. Do we have a deal?"
    mS "Alright. Deal."
    smatt "Good. My pointman Reginald will brief and supervise."
    hide engineer_stream
    show goon_stream at stream_right with dissolve
    "One of the henchmen nods to us."
    $ chatter_list.append(vig2_sc4_comment9)
    smatt "I'll see you ladies later. It's been lovely catching up."
    "Matticus waves as he leaves the warehouse."
    "Reginald turns to us."
    goon "Alright. So first we need to get the tracking coordinates for this ship."
    goon "They're stored at a communications array on the outskirts of the city."
    $ chatter_list.append(vig2_sc4_comment10)
    mS "Matticus doesn't have the target's location?"
    pS "Yeah why is this our job? I thought the {i}king{/i} would have mooks to do that for him."
    goon "His guys can't be seen doing this."
    pS "How reassuring."
    goon "We don't have time for this. Let's get to the shuttle."
    mS "Agreed."
    jump approachingBase

###SCENE 5###
label approachingBase():
    scene targetbase_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show pilot_stream at stream_left with dissolve
    show engineer_stream at stream_right with dissolve
    "The shuttle lands in the grass near the base."
    "Hardly a base, more of an outpost with a big antenna."
    pS "I forgot how cramped these cheapo shuttles are."
    enS "Fitting for a cheapo planet like this."
    mS "Pipe down you two. Focus up."
    "Jennica and Teresa nod to me."
    show mac_stream at stream_center
    "The brief silence is broken by MAC."
    macS "What is a \"cheapo planet?\" My database does not have a category \"cheapo.\""
    enS "What about that?"
    "Teresa gestures towards MAC."
    enS "Should we leave it?"
    mS "We're gonna have to bring him along. It's not ideal, but if he gets taken from the shuttle then all of this is pointless."
    enS "True. However it is equally pointless if it gets damaged beyond repair."
    enS "And we \"lost\" the last mechanic we knew who could help out with that."
    mS "I'll protect him. It's my responsibility."
    mS "MAC you're going to stick to me like glue, got it?"
    macS "Like an adhesive? Would that not damage your clothes and person?"
    "Jennica chuckles under her breath."
    $ chatter_list.append(vig2_sc5_comment1)
    mS "No MAC, you need to stay close to me. It's dangerous in there and I need to protect you."
    macS "This is a dangerous place? Are these people \"skeeves\" like Matticus?"
    menu:
        "They're bad people":
            $ chatter_list.append(vig2_sc5_comment2)
            mS "Yes. They're bad people."
            $ chatter_list.append(vig2_sc5_comment3)
            macS "Understood. I will stay close to Moze."
            $ chatter_list.append(vig2_sc5_comment4)
            mS "Good. Let's move."
        "Maybe":
            mS "I don't know. But they will try to hurt us."
            $ chatter_list.append(vig2_sc5_comment2)
            macS "Why do we need to work for \"skeeves?\""
            mS "We don't have the time to discuss it right now."
            $ chatter_list.append(vig2_sc5_comment5)
            mS "You just need to stay close to me."
            macS "Understood. I will stay close to Moze."
            mS "Good. Let's move."
            hide mac_stream with dissolve
    show goon_stream at stream_center with Dissolve(0.5)
    pS "So what's the plan?"
    goon "Plan? This is your operation. We just gotta get to the main datacenter computer."
    enS "Do you even know where that is?"
    goon "Yeah. In there somewhere. This is Gibian V. There are maybe three or four rooms in there."
    goon "How much traffic do you think comes here?"
    pS "Maybe we can get in and out without drawing any attention?"
    goon "Seems pointless, but I don't give a shit. It's your job."
    enS "You're not coming with us?"
    goon "You think I get paid enough to get shot at? As if."
    "Reginald lights a cigarette and walks off."
    $ chatter_list.append(vig2_sc5_comment6)
    enS "I'm inclined to agree with him. We don't know how much firepower they'll have in there."
    enS "I say we go in heavy and take them out expeditiously. Minimize the hazards to ourselves and the cargo."
    pS "The fewer people who know we were here the better. And an explosion might tip a few folks off"
    enS "On Gibian V? I doubt this news makes it off-world."
    pS "What do you think Cap?"
    menu:
        "A full offensive is more tactically sound":
            $ gunsBlazing = True
            $ outlaw += 2
            "We don't know what kind of security we're walking into."
            $ chatter_list.append(vig2_sc6_out_comment1)
            "Getting caught off guard is a risk we can't afford to take."
            mS "I agree with Teresa. We can't know what's waiting for us inside."
            mS "We'll get in and out quick and dirty."
            $ chatter_list.append(vig2_sc6_out_comment2)
            pS "Your call Cap."
            enS "I'll whip up a door breeching charge."
            mS "I'll take point, you two cover me when I go in."
            $ viewCount += 1
            $ chatter_list.append(vig2_sc6_out_comment3)
            "Teresa and Jennica nod in agreement."
            "I wave over Reginald."
            goon "So what are we doing?"
            mS "Going in the front door guns blazing."
            goon "Legends. This should be entertaining."
            "Teresa signals to me that she's ready."
            mS "Alright let's get to it."
            jump commsBase_OUT1
        "Better to be as quiet as possible":
            $ marshal += 2
            $ gunsBlazing = False
            $ chatter_list.append(vig2_sc6_mar_comment1)
            "Quick and quiet would be safer."
            mS "Jennica's right, lets try to avoid attention."
            enS "Very well. I'll get to work disabling the alarm."
            $ chatter_list.append(vig2_sc6_mar_comment2)
            $ shnzi = False
            $ viewCount -= 1
            pS "There's a fire escape on the left side. That could be our way in."
            mS "Good. Remember, quick and quiet."
            "Jennica and Teresa nod in agreement."
            $ chatter_list.append(vig2_sc6_mar_comment3)
            "I wave Reginald over."
            mS "We're going in quiet."
            "Reginald chuckles."
            $ chatter_list.append(vig2_sc6_mar_comment4)
            goon "Really? I didn't think Ex-Snakehawks would be so demure."
            goon "Whatever. I'll follow behind you."
            jump commsBase_MAR1

####SCENE 6####
label commsBase_MAR1():
    scene baseinfiltration_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show engineer_stream at stream_right with Dissolve (0.3)
    "As we approach the base, Teresa signals to stay quiet."
    "After climbing up the fire escape, Teresa takes out a device and affixes it to the door."
    "The devices begins to beep and its lights flicker on and off."
    "Teresa mutters under her breath."
    enS "Child's play."
    "The door unlocks and pops ajar."
    hide engineer_stream with dissolve
    "We're inside. The base seems even smaller on the inside, and lightly guarded."
    "Its just a few stairs down from the upper level to the ground floor."
    "The stairs lead to a hallway."
    "We turn the corner, and run face-first into a lone guard on patrol."
    "He jumps up, startled, and begins raising his hand..."
    "My instincts take over."
    menu:
        "My instincts take over."
        "Stun him":
            "I instantly flick my blaster to stun before shooting him in the chest."
            $ chatter_list.append(vig2_sc6_mar_choice1_stun_comment1)
            "He can barely react as the stunning blast ripples through his body, incapacitating him."
            "As he slumps to the ground, his eyes stare back at me with... confusion?"
            "He seems completely dumbfounded as to why we're here."
            goon "Keep moving!"
            mS "I know!"
            "I can't afford to hesitate."
            jump commsBase_MAR2
        "Bluff":
            $ marshal += 1
            mS "Hey! How's it going?"
            "His hand raises into a wave as he greets us"
            hsguard1 "Hey! Sorry I don't think we've met."
            mS "Yeah, we're the new security recruits. Just getting familiarized now for our shift tomorrow."
            hsguard1 "We have money for new recruits?"
            mS "Yeah I was shocked too. I'll see you around?"
            hsguard1 "Sure."
            "He continues along his patrol, oblivious."
            $ chatter_list.append(vig2_sc6_mar_choice1_bluff_comment1)
            pS "That was too easy."
            mS "Tell me about it."
            jump commsBase_MAR2

label commsBase_MAR2():
    scene baseinfiltration_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show engineer_stream at stream_right with Dissolve(0.5)
    show pilot_stream at stream_left with Dissolve(0.5)
    show goon_stream at stream_center with Dissolve(0.5)
    "At the end of the hall, we arrive at a door."
    "Cracking the door ajar, I'm able to get a look at whats on the other side."
    "There is a typical security desk, manned by three guards."
    "They don't have much in the way of firepower, just their sidearms."
    "And they aren't so focused either. Shouldn't be too hard to get past them."
    "We could probably bust in and stun them all without an issue."
    "But that might get a little hairy."
    "A fake radio call would probably get them all to leave too."
    "They might come back though."
    menu:
        "Knock them out":
            $ outlaw += 1
            "We can't risk them coming back and finding us."
            $ chatter_list.append(vig2_sc6_mar_choice2_stun_comment1)
            "We need to neutralize these guys."
            "I signal to Jennica and Teresa that we're going to stun them."
            "I'll take the one on the right, Jennica will get the middle one, and Teresa the left."
            "Jennica and Teresa take position behind me."
            "Once everyone's ready, I kick the door open."
            $ chatter_list.append(vig2_sc6_mar_choice2_stun_comment2)
            "It's over in a flash, the guards barely have time to react."
            "Like clockwork, they all fall to the ground."
            pS "This all feels way too easy."
            enS "Why is that a bad thing?"
            $ chatter_list.append(vig2_sc6_mar_choice2_stun_comment3)
            enS "I rather like things being easy."
            "Reginald signals us to follow him."
            goon "The main datacenter is through this door here."
            "Reginald opens the door."
            jump commsBase_DataCenter
        "Go with a diversion":
            $ marshal += 1
            "We can get through without firing a shot."
            "Quick and quiet."
            "I signal to Teresa to prepare a fake radio diversion."
            "She nods, and begins inputting code into her computer."
            $ chatter_list.append(vig2_sc6_mar_choice2_dist_comment1)
            "In an instant, all three of the guard's radios begin blaring with a distress call."
            "A sense of urgency hits them as they begin running out of the room to the source of the call."
            "Once their out, I wave over Reginald."
            goon "Wow so much work for what?"
            pS "This all feels way too easy."
            $ chatter_list.append(vig2_sc6_mar_choice2_dist_comment2)
            enS "Why is that a bad thing?"
            enS "I rather like things being easy."
            "Reginald signals us to follow him."
            goon "The main datacenter is through this door here."
            "Reginald opens the door."
            jump commsBase_DataCenter

label commsBase_OUT1():
    scene baseinfiltration_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show engineer_stream at stream_right with Dissolve(0.5)
    show pilot_stream at stream_left with Dissolve(0.5)
    "Teresa begins placing the charge on the front door."
    "We line up on either side of the entrance."
    show mac_stream at stream_center with Dissolve(0.5)
    "I notice MAC looks a little worried."
    mS "Just stay close to me."
    macS "Affirmative. Sticking to Moze."
    $ chatter_list.append(vig2_sc6_out_comment5)
    hide mac_stream with Dissolve(0.5)
    "I give the signal to Teresa."
    hide pilot_stream
    hide engineer_stream
    "With a deafening bang, the door is obliterated into countless pieces."
    "An instant later I'm running through the smoke."
    "MAC's footsteps clink behind me."
    "I make a dive for a piece of rubble as shots fly around me."
    $ chatter_list.append(vig2_sc6_out_comment6)
    "I peek out from cover."
    $ chatter_list.append(vig2_sc6_out_comment7)
    "Two of the guards are dead already from the explosion."
    $ chatter_list.append(vig2_sc6_out_comment8)
    "Two more are shooting at us from the other side of the room."
    $ chatter_list.append(vig2_sc6_out_comment9)
    "I think I have a good shot on one of them."
    menu:
        "Shoot to kill":
            $ outlaw += 1
            "I take a deep breath and fire my blaster."
            "The guard's head snaps back before he slumps over."
            $ chatter_list.append(vig2_sc6_out_shoot_comment1)
            $ chatter_list.append(vig2_sc6_out_shoot_comment2)
            $ chatter_list.append(vig2_sc6_out_shoot_comment3)
            "I hear shots ringing out from behind me."
            $ chatter_list.append(vig2_sc6_out_shoot_comment4)
            pS "Nice shooting!"
            show pilot_stream at stream_left with Dissolve(0.2)
            "Jennica dives into cover next to me."
        "Suppressing fire":
            $ marshal += 1
            "Too risky."
            $ chatter_list.append(vig2_sc6_out_suppress_comment1)
            "I fire a few more shots from cover."
            $ chatter_list.append(vig2_sc6_out_suppress_comment2)
            "Shots ring out from behind me, hitting one of the guards."
            pS "Thanks for the cover!"
            show pilot_stream at stream_left with Dissolve(0.2)
            "Jennica dives into cover next to me."
    mS "There's only one left!"
    mS "Teresa has him pinned down"
    pS "I'll get him!"
    "Jennica leaps out of cover and begins running towards the guard."
    "Three more guards emerge from the door, guns drawn."
    pS "Shit! We got company!"
    $ chatter_list.append(vig2_sc6_out_comment11)
    "Jennica takes cover behind another piece of rubble."
    "I hear shots whizzing over my head as the guards fire at us."
    "I pull out a grenade and lob it at the guards."
    $ chatter_list.append(vig2_sc6_out_comment12)
    hsguard1 "GRENADE! GET DOWN!"
    "Two of the guards are able to dive out of the way in time."
    $ chatter_list.append(vig2_sc6_out_comment13)
    "The grenade detonates with an unnerving concussive thump."
    "I peek out of cover and spot one of the guards running."
    "Jennica fires her blaster, hitting the fleeing guard."
    $ chatter_list.append(vig2_sc6_out_comment14)
    "Teresa continues shooting."
    "Only one guard is left."
    $ chatter_list.append(vig2_sc6_out_comment15)
    show guard1_stream at stream_right with Dissolve(0.4)
    hsguard1 "I surrender!"
    mS "Stand up and put your hands where I can see them!"
    "The guard complies."
    hsguard1 "Please Miss, just let me go, I promise I'll never breathe a word of this to anyone."
    "He's terrified. This is probably the first time anyone's shot at him."
    $ chatter_list.append(vig2_sc6_out_comment16)
    pause 0.5
    $ chatter_list.append(vig2_sc6_out_comment17)
    pause 0.5
    $ chatter_list.append(vig2_sc6_out_comment18)
    menu:
        "This isn't his fault":
            $ marshal += 2
            $ chatter_list.append(vig2_sc6_out_spare_comment1)
            "This guy was just doing his job."
            mS "Alright."
            $ chatter_list.append(vig2_sc6_out_spare_comment2)
            hsguard1 "Thank you miss! I-"
            hide guard1_stream with Dissolve (0.1)
            "A shot rings out and the guard falls over, lifelessly."
            show goon_stream at stream_center with dissolve
            "Reginald stands over the corpse."
            $ chatter_list.append(vig2_sc6_out_spare_comment3)
            goon "Come on. We're almost done."
            $ chatter_list.append(vig2_sc6_out_spare_comment4)
            pS "What the hell!"
            mS "He didn't need to die!"
            $ chatter_list.append(vig2_sc6_out_spare_comment5)
            show engineer_stream at stream_right with Dissolve(0.5)
            enS "Better him than us I suppose."
            $ chatter_list.append(vig2_sc6_out_spare_comment6)
            "Jennica scowls at Teresa."
            goon "We haven't got all day!"
            $ chatter_list.append(vig2_sc6_out_spare_comment7)
            "Reginald approaches and opens the door to the datacenter."
        "I can't risk survivors":
            $ outlaw += 2
            "The less witnesses the better."
            $ chatter_list.append(vig2_sc6_out_execute_comment1)
            $ chatter_list.append(vig2_sc6_out_execute_comment2)
            "I raise my blaster."
            $ chatter_list.append(vig2_sc6_out_execute_comment3)
            hsguard1 "Miss I have kids."
            $ chatter_list.append(vig2_sc6_out_execute_comment4)
            hide guard1_stream with Dissolve(0.1)
            "His body slumps to the ground lifelessly."
            $ chatter_list.append(vig2_sc6_out_execute_comment5)
            $ chatter_list.append(vig2_sc6_out_execute_comment6)
            $ chatter_list.append(vig2_sc6_out_execute_comment7)
            pS "Damn Cap. That was cold."
            $ chatter_list.append(vig2_sc6_out_execute_comment8)
            $ chatter_list.append(vig2_sc6_out_execute_comment9)
            $ chatter_list.append(vig2_sc6_out_execute_comment10)
            enS "Better safe than sorry."
            $ chatter_list.append(vig2_sc6_out_execute_comment11)
            show goon_stream at stream_right with dissolve
            "Reginald emerges through the door."
            $ chatter_list.append(vig2_sc6_out_execute_comment12)
            $ chatter_list.append(vig2_sc6_out_execute_comment13)
            pS "Nice of you to join us."
            $ chatter_list.append(vig2_sc6_out_execute_comment14)
            goon "Come on. We're almost done."
            $ chatter_list.append(vig2_sc6_out_execute_comment15)
    jump commsBase_DataCenter

###SCENE 7###
label commsBase_DataCenter():
    scene datacenter_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    "The datacenter is underwhelming."
    "At the center of the small room is a computer workstation, with numerous monitors displaying a variety of shipments and other logistic info."
    show worker_stream at stream_center with dissolve
    "Sitting at the workstation is a scrawny technician in glasses, terrified."
    worker "What are you doing here?!"
    show goon_stream at stream_right with Dissolve(0.2)
    "Before we have a chance to say anything, Reginald is in his face."
    goon "Shut it! Whats the next shipment's tracking coordinates?!"
    worker "The aid shipm-"
    "Reginald shoves his gun into the technician's face."
    goon "NOW!"
    show pilot_stream at stream_left with Dissolve(0.4)
    pS "What did he say?"
    worker "Why do you want the tracking coordinates of an aid shipment?"
    $ chatter_list.append(vig2_sc7_comment1)
    pS "Yeah Reginald, why {i}do{/i} we want the tracking coordinates of an aid shipment?"
    "Reginald sighs exasperatedly."
    goon "I don't get paid enough for this."
    goon "The shipment's going to a town called Sallent."
    goon "They haven't paid their protection money this month."
    $ chatter_list.append(vig2_sc7_comment2)
    goon "The boss wants to send a \"message\" to them and any other would-be heroes who don't think they need protection."
    $ chatter_list.append(vig2_sc7_comment3)
    pS "I knew this was too easy!"
    hide worker_stream
    show mac_stream at stream_center with dissolve
    macS "They were not skeeves?"
    pS "They were rent-a-cops!"
    hide mac_stream
    hide pilot_stream
    show engineer_stream at stream_left with Dissolve(0.4)
    enS "Well they were still cops."
    goon "Look. This is the mission. Don't act like you've never done worse."
    goon "I've heard stories of what you all did."
    hide engineer_stream with dissolve
    "Reginald turns to look me in the eyes."
    goon "Especially {i}you{/i}."
    mS "So what happens if the town doesn't get that aid."
    show worker_stream at stream_center with Dissolve(0.4)
    worker "Sallent? There's an outbreak of gray fever there. They need that medicine!"
    hide worker_stream with Dissolve (0.4)
    goon "People from a town you had never heard of until now might die."
    goon "You really care that much about them?"
    $ chatter_list.append(vig2_sc7_comment4)
    show engineer_stream at stream_center with Dissolve(0.4)
    enS "As much as I loathe to admit it, I agree."
    enS "We came here to cover our trail."
    $ chatter_list.append(vig2_sc7_comment5)
    enS "If we renege on the deal now, we'll be no closer to doing that, and will have made an enemy in Matticus."
    enS "All the people we save here won't matter if we get caught!"
    show pilot_stream at stream_left with Dissolve (0.3)
    pS "They're people!"
    pS "Just because they were born here doesn't make them disposable!"
    pS "Can you really go through with this Cap?"
    $ chatter_list.append(vig2_sc7_comment6)
    "MAC looks worried." 
    $ chatter_list.append(vig2_sc7_comment7)
    menu:
        "This has gone too far":
            $ marshal += 3
            "This is just plain wrong."
            "We have to be better."
            "Or at least we have to try to be."
            jump commsBase_DataCenter_MAR
        "We don't have a choice":
            $ outlaw += 3
            "We've come this far."
            $ chatter_list.append(vig2_sc7_out_comment1)
            "We have to do this."
            mS "We don't have a choice anymore."
            jump commsBase_DataCenter_OUT

label commsBase_DataCenter_MAR():
    scene datacenter_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show pilot_stream at stream_left with Dissolve(0.5)
    show engineer_stream at stream_right with Dissolve (0.5)
    mS "We can't do this."
    pS "Cap?"
    $ chatter_list.append(vig2_sc7_mar_comment1)
    mS "We've come this far because we wanted to try and make this galaxy better."
    mS "How can we honestly say that if we follow through with this?"
    enS "Yes, but what does that matter if we get caught and bigcorp takes the robot back?"
    pS "We'll have tried."
    $ chatter_list.append(vig2_sc7_mar_comment2)
    enS "I don't like this any more than you two, but what's the alternative?"
    enS "If we don't cover our tracks, The Deadeye will be on our asses in no time! We need Matticus' access to the security database."
    pS "He's not the only one with access!"
    enS "Yes access to secure databases is easy to find and widespread! How could I forget?!"
    enS "That's what \"secure\" means right?"
    "The technician perks up. He begins gingerly raising his hand before he's interrupted by Reginald."
    show goon_stream at stream_center with Dissolve(0.3)
    goon "Oh you have got to be kidding me."
    "Reginald points his blaster at me."
    goon "How did the most infamous outlaw merc group become soft as baby shit?"
    $ chatter_list.append(vig2_sc7_mar_comment3)
    goon "You really think I'm gonna let you just bail on this deal?"
    "I notice Jennica reaching for her blaster."
    menu:
        "Signal Jennica to stun him":
            $ outlaw += 1
            "I look over to Jennica and give her the signal."
            "Reginald is struck by the stun bolt. His body seizes up and shakes before he falls to the floor, incapacitated."
            hide goon_stream with Dissolve(0.2)
            pS "Wish we coulda done that hours ago."
            $ chatter_list.append(vig2_sc7_mar_stun_comment1)
            "I can see the burning hatred in his eyes, as if to say, \"This isn't over!\""
            enS "I suppose the die is cast then."
            enS "So where are we to find someone to help cover our tracks now?"
            pS "Beats me."
        "Bribe Reginald":
            $ marshal += 1
            "I signal to Jennica to wait."
            mS "How much do you really like working for Sav?"
            goon "Like?"
            mS "You strike me as someone with flexible attitudes towards allegiance, not a true believer."
            "Reginald begins to lower his blaster."
            goon "Go on."
            mS "What would it cost for you to get off world? I take it you have ambitions bigger than Gibian V?"
            "Reginald's brow furrows."
            goon "A couple grand I guess?"
            pS "A couple grand?!"
            $ chatter_list.append(vig2_sc7_mar_bribe_comment1)
            goon "I need to make it off world in one piece. Money don't spend if I'm dead."
            mS "We can afford that."
            "I reach into my pocket and grab 2000 credits."
            goon "Pleasure doing business."
            $ chatter_list.append(vig2_sc7_mar_bribe_comment2)            
            "Reginald looks us over one more time before turning around and briskly leaving."
            hide goon_stream with dissolve
            pS "{i}Can{/i} we afford that?"
            mS "I hope so."
            enS "We have bigger problems. Where are we to find someone to help us cover our tracks now?"
    show worker_stream at stream_center with Dissolve(0.5)
    worker "I uh... Couldn't help but overhear your predicament..."
    enS "Eavesdropping are we?" 
    worker "You're holding me uh... hostage."
    mS "Yeah. Sorry about that. You were saying?"
    "The technician shifts around uneasily."
    worker "I might be able to help you."
    enS "Oh?"
    worker "I have some access to the bigcorp security database. Not as much as Matticus obviously, but I can get in and spread a few reports."
    pS "Well butter my biscuit!"
    mS "How do you have access? Sounds awfully convenient."
    worker "The Intergalactic Humanitarian Society has observer access to the database, to stay informed about pirates and solar storm sightings."
    $ chatter_list.append(vig2_sc7_mar_comment4)
    worker "Once I'm in, I just need to spoof the ID to make it look like I'm an ordinary security grunt."
    enS "Where did you learn how to do all of this?"
    "The technician continues to shift uneasily."
    worker "I... might use the Society's remote shipments to smuggle cargo."
    worker "The tariffs are insane! Matticus isn't exactly a free trade advocate."
    "This feels too good to be true."
    $ chatter_list.append(vig2_sc7_mar_comment5)
    mS "Why are you helping us?"
    if gunsBlazing == False:
        worker "You care enough about strangers from a small to make an enemy out of Matticus."
        worker "You're obviously good people. And you risked your neck to spare all of my colleagues here."
        worker "I saw on the security cameras."
        "The technician smiles at me."
        $ chatter_list.append(vig2_sc7_mar_comment6)
    if gunsBlazing == True:
        worker "What choice do I have?!"
        worker "You killed everyone here! I saw on the security cameras."
        "The technician sighs."
        worker "But you're better than Matticus. At least you refused to let all those people to die of gray fever."
        $ chatter_list.append(vig2_sc7_mar_comment7)
        worker "Better the lesser evil."
    mS "Fair enough. So what do you need to do this?"
    $ chatter_list.append(vig2_sc7_mar_comment8)
    worker "I can do it right here. I just need your ship's registration code, planet of origin and operating name."
    enS "How can we know you're not going to betray us?"
    worker "You can't. You're going to have to trust me."
    enS "Worrisome."
    worker "Maybe, but you don't really have a choice."
    mS "He's not wrong. We burned the bridge with Matticus, this is our only option."
    pS "Can't look a gift horse in the mouth. For better or worse."
    "Teresa sighs in resignation."
    enS "Very well."
    worker "So we have a deal?"
    "I reach out and shake his hand."
    mS "Deal. Don't screw us."
    "The technician nods. He begins typing a series of commands into the computer."
    worker "Alright it should be set up. It'll begin sending in the reports once you're off world."
    mS "Great. Needless to say, we were never here."
    $ chatter_list.append(vig2_sc7_mar_comment9)
    "The technician smiles and winks at me."
    worker "I don't know who you're talking about. I wasn't even working that night!"
    jump vig2epilogue_MAR

label commsBase_DataCenter_OUT():
    scene datacenter_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show pilot_stream at stream_left
    show engineer_stream at stream_right    
    pS "And let all those people die?!"
    enS "Thousands more will die as well if they get MAC."
    if shnzi == True:
        $ chatter_list.append(vig2_sc7_out_comment2_shnzi)
    else:
        $ chatter_list.append(vig2_sc7_out_comment2_AFK)
    mS "I don't like this any more than you do Jen, but we're out of options."
    pS "Damnit Cap."
    $ chatter_list.append(vig2_sc7_out_comment3)
    $ chatter_list.append(vig2_sc7_out_comment4)    
    enS "If you have a better way to cover our trail please do share."
    pS "I thought we were trying to do better than this."
    $ chatter_list.append(vig2_sc7_out_comment5)
    mS "We can't do better if we're in jail, and considering how close we got last time..."
    mS "I can't lose you guys. You're all I've got."
    $ chatter_list.append(vig2_sc7_out_comment6)
    hide pilot_stream
    hide engineer_stream
    show goon_stream at stream_left with Dissolve(0.5)
    goon "Are we done with this yet?"
    goon "I ain't got all day."
    $ chatter_list.append(vig2_sc7_out_comment7)
    mS "Yeah. Lets get the tracking code and get out of here."
    $ chatter_list.append(vig2_sc7_out_comment8)
    goon "Good."
    $ chatter_list.append(vig2_sc7_out_comment9)
    "Reginald turns to the technician."
    $ chatter_list.append(vig2_sc7_out_comment10)
    show worker_stream at stream_center with Dissolve(0.5)
    goon "I'm not going to ask nicely again. The tracking code, now."
    $ chatter_list.append(vig2_sc7_out_comment11)
    "The technician looks terrified as he clicks through the various menus on the computer."
    worker "44s-73f-94c-21o."
    $ chatter_list.append(vig2_sc7_out_comment12)
    $ chatter_list.append(vig2_sc7_out_comment13)
    worker "You realise these people have nothing right?"
    $ chatter_list.append(vig2_sc7_out_comment14)
    $ chatter_list.append(vig2_sc7_out_comment15)
    "Reginald logs the code into his agenda."
    goon "I don't get paid to realise things."
    $ chatter_list.append(vig2_sc7_out_comment16)
    $ chatter_list.append(vig2_sc7_out_comment17)
    $ chatter_list.append(vig2_sc7_out_comment18)
    show engineer_stream at stream_right with Dissolve(0.5)
    enS "What do we do with the Technician?"
    if shnzi == True:
        $ chatter_list.append(vig2_sc7_out_comment19_shnzi)
    else:
        $ chatter_list.append(vig2_sc7_out_comment19_shnzi)
    goon "I'll leave that to you. Meet me back at the shuttle."
    $ chatter_list.append(vig2_sc7_out_comment20)
    $ chatter_list.append(vig2_sc7_out_comment21)
    "Reginald walks out of the room."
    $ chatter_list.append(vig2_sc7_out_comment22)
    $ chatter_list.append(vig2_sc7_out_comment23)
    $ chatter_list.append(vig2_sc7_out_comment24)
    hide goon_stream with dissolve
    show pilot_stream at stream_left with Dissolve(0.5)
    menu:
        "Convince him not to say anything":
            $ marshal += 1
            "We don't need to hurt him. Just need to make him think we will."
            mS "Look, it's in your best interest that you forget everything that happened here."
            $ chatter_list.append(vig2_sc7_out_coerce_comment1)
            worker "What?"
            mS "Spreading word of this around is just gonna make things more difficult."
            mS "For us..."
            $ chatter_list.append(vig2_sc7_out_coerce_comment2)
            "I pull out my blaster and slowly drag the muzzle across his chest."
            mS "And for you. We really don't want to have to come back and deal with you."
            $ chatter_list.append(vig2_sc7_out_coerce_comment3)
            mS "You don't want that either."
            "The technician is shaking, paralyzed with fear."
            $ chatter_list.append(vig2_sc7_out_coerce_comment4)
            mS "Do we understand each other?"
            worker "Yes! I won't breathe a word of this to anyone else!"
            mS "Glad to hear it. Let's move."
            hide worker_stream with dissolve
            pS "Damn Cap, that was dark."
            enS "If it works it works."
        "Kill him":
            $ outlaw += 2
            $ chatter_list.append(vig2_sc7_out_execute_comment1)
            "Only one way to make sure this never leaves the building."
            $ chatter_list.append(vig2_sc7_out_execute_comment2)
            "I raise my blaster up to the technician's face."
            worker "Wait! You don't have to do-"
            hide worker_stream with Dissolve(0.1)
            $ chatter_list.append(vig2_sc7_out_execute_comment3)
            "The shot rings out. The technician's body slumps to the floor, lifeless."
            pS "Damn Cap, that was dark."
            $ chatter_list.append(vig2_sc7_out_execute_comment4)
            enS "Perhaps. But now we can be certain he won't expose us."
            mS "Enough blabbing. Lets go."
    show goon_stream at stream_center with Dissolve(0.5)
    "Reginald is waiting by the shuttle for us."
    goon "All sorted?"
    mS "Yeah. So how are we doing this?"
    goon "We need to make sure the shot can't be traced back to us."
    goon "We'll make the incerception, and then ditch this shuttle in the desert."
    enS "How are we getting out of the desert?"
    goon "The boss has a crew waiting for us out there. They'll take care of the disposal."
    pS "And the firepower?"
    goon "This shuttle has a bigcorp missle loaded on it. When they try and trace it, it'll just lead back to their own factories."
    mS "Sounds simple enough."
    goon "Swell."
    jump shuttleDestruction

label shuttleDestruction():
    scene orbit_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show goon_stream at stream_center with dissolve
    show pilot_stream at stream_left with dissolve
    show engineer_stream at stream_right with dissolve       
    "Once we're in low orbit, it's only a matter of time before the shuttle shows up."
    goon "You're sure we're in the right spot?"
    pS "You think I can't read a tracking code?"
    goon "I think you don't want to."
    "Jennica scoffs."
    enS "My scanner says it's incoming."
    "Out of the corner of my eye, I see a small light approaching us."
    "An unmanned aid ship comes into view. It's small, barely larger than the shuttle."
    goon "That's it."
    $ chatter_list.append(vig2_sc7_out_comment25)
    mS "Alright Jen."
    pS "You sure about this Cap?"
    mS "Yeah. Hit it."
    $ chatter_list.append(vig2_sc7_out_comment26)
    "Jennica pulls the trigger."
    "A rush of momentum shakes the shuttle as the missle flies out from under it."
    "The missle races across the sky before colliding with the ship, obliterating it in a fireball."
    "Pieces of debris scatter from the site of impact."
    "Jennica sighs."
    $ chatter_list.append(vig2_sc7_out_comment27)
    pS "I hope this is worth it."
    $ chatter_list.append(vig2_sc7_out_comment28)
    jump vig2epilogue_OUT

label vig2epilogue_MAR():
    scene bridge_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master 
    show engineer_stream at stream_right with dissolve
    show pilot_stream at stream_left with dissolve  
    enS "Thank goodness that's over. If I never make it back to Gibian V it'll be too soon."
    $ chatter_list.append(vig2_epilogue_mar_comment1)
    pS "I couldn't agree more! Although it felt good sticking it to Matticus like that."
    pS "Wish I coulda seen his face..."
    $ chatter_list.append(vig2_epilogue_mar_comment2)
    "The meeting is interrupted by a ship intercom alert."
    shipcom "Incoming call from Magistrate Savlian Matticus."
    mS "Be careful what you wish for."
    mS "Accept."
    show matticus_stream at stream_center with Dissolve(0.5)
    "Matticus appears on the ship computer. He is incensed."
    $ chatter_list.append(vig2_epilogue_mar_comment3)
    $ chatter_list.append(vig2_epilogue_mar_comment4)
    smatt "You idiots! You don't know how much you just screwed me."
    mS "Sorry Sav I'm picking up some bullshit on this line, what was that?"
    smatt "I should've known you didn't have the stomach for real work."
    $ chatter_list.append(vig2_epilogue_mar_comment5)
    mS "Real work like blowing up medicine for some bumpkins?"
    smatt "Since when do you care about anyone but yourself Moze?!"
    smatt "You've done worse. This wouldn't have even cracked your top ten!"
    mS "Is that it?"
    $ chatter_list.append(vig2_epilogue_mar_comment6)
    "I hear Jennica snicker behind me."
    smatt "Don't worry. You'll get yours. You think I got to where I am by letting people screw me over?"
    mS "Lose this channel Teresa."
    $ chatter_list.append(vig2_epilogue_mar_comment7)
    enS "With pleasure."
    hide matticus_stream with dissolve
    "The screen powers off."
    pS "Maybe we shouldnta added insult to injury?"
    $ chatter_list.append(vig2_epilogue_mar_comment8)
    mS "Maybe. But didn't that feel good?"
    "Jennica smiles and chuckles warmly"
    pS "Like fresh underwear after a shower."
    mS "Alright, do you two have anything else to report?"
    enS "All systems are functional Captain."
    pS "Steady as she goes cap."
    mS "Good. I'll be in my quarters."
    hide engineer_stream with Dissolve(0.5)
    hide pilot_stream with Dissolve(0.5)
    "I move to leave the briefing room when I feel a tug on my sleeve."
    show mac_stream at stream_center with Dissolve(0.5)
    macS "May I ask a question?"
    mS "Of course MAC."
    macS "I am confused about the nature of our mission? Why do we need to hide if we are good people?"
    menu:
        "The people in power are bad":
            mS "Sometimes the people in power are bad, and to do good we need to hide from them."
            macS "But we broke the law many times. How can we be doing good by breaking the law?"
            mS "Laws aren't always good. Sometimes they're put in place by bad people."
            macS "But how do you know if a law is bad?"
            mS "That's up to you to decide. That's why we're outlaws. There's no one-size-fits-all rule."
            mS "The universe is just more complicated than that."
            macS "I see. The universe is confusing."
            mS "Agreed."
        "The universe is bad and we can't trust anyone":
            mS "The universe is a dangerous place. We can never know who's looking to stab us in the back."
            mS "Better to hide than risk that I'd say."
            macS "But we broke the law many times. How can we be good if we contribute to the danger of the universe?"
            mS "The universe was dangerous before us and it'll be the same after we're gone. Following the rules won't change that."
            mS "To change bad things, sometimes we have to do dangerous things."
            macS "I see. The universe is confusing."
            mS "Agreed."
    $ narrator = reg_narrator
    $ macroNarration = True
    $ chatter_list.append(vig2_epilogue_mar_comment9)
    hide mac_stream with dissolve
    player "Wow..."
    player "Guess that's the end of the chapter, seems like a good spot to end."
    player "Alright everyone, thank you all for coming out! See ya next time!"
    hide bridge_stream with dissolve
    "You quit out of the game and watch as the people in the stream discuss the game."            
    $ chatter_list.append(vig2_epilogue_mar_comment10)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_mar_comment11)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_mar_comment12)
    pause 0.5
    $ marshal += 10
    "As the chat winds down, you take off your headset, and sign off of Twitch."
    hide screen streamChat
    hide screen streamDetails
    scene black with dissolve
    jump vig2_macro_start

label vig2epilogue_OUT():
    scene bridge_stream at topleft onlayer master with dissolve
    show streamview at center onlayer master
    show engineer_stream at stream_right with dissolve
    show pilot_stream at stream_left with dissolve 
    "The ship computer powers on."
    $ chatter_list.append(vig2_epilogue_out_comment1)
    shipcom "Incoming call from Magistrate Savlian Matticus."
    mS "Accept."
    $ chatter_list.append(vig2_epilogue_out_comment2)
    show matticus_stream at stream_center with dissolve
    "Matticus appears on the ship computer."
    smatt "Oh Moze. Its been so wonderful working with you again."
    smatt "We have to catch up some time!"
    "Jennica is visibly annoyed."
    mS "And your end of the deal?"
    $ chatter_list.append(vig2_epilogue_out_comment3)
    smatt "It's already done! As far as anyone is knows, the Oakley's doing a grand tour of the Mauritz nebula."
    $ chatter_list.append(vig2_epilogue_out_comment4)
    pS "Make sure he's not lying Teresa."
    "Teresa nods and begins typing on her computer."
    $ chatter_list.append(vig2_epilogue_out_comment5)
    smatt "Oh Jenn. You're a real ice queen."
    smatt "So cold."
    "Matticus mimes himself shivering."
    $ chatter_list.append(vig2_epilogue_out_comment6)
    enS "It's true. Our logged coordinates are on the other side of the galaxy right now."
    smatt "See? I would never deceive you guys. We old Snake Hawks need to look out for each other."
    "Jennica rolls her eyes."
    smatt "Alright, now that our business has concluded, I'm going to have a drink."
    smatt "Let me know when you're done playing hero with the robot Moze. I've got lots more work where that came from."
    $ chatter_list.append(vig2_epilogue_out_comment7)
    mS "Goodbye Sav."
    smatt "Think about it."
    $ chatter_list.append(vig2_epilogue_out_comment8)
    "Matticus winks before the screen powers off."
    hide matticus_stream with dissolve
    enS "Well at least we have some breathing room."
    $ chatter_list.append(vig2_epilogue_out_comment9)
    enS "Ama won't trouble us for the foreseeable future."
    pS "I suppose. The Ama we ran with won't chase red herrings for long."
    $ chatter_list.append(vig2_epilogue_out_comment10)
    mS "Agreed. We ought to keep an eye out."
    mS "Anything else to report?"
    enS "All systems are functional Captain."
    pS "Steady as she goes cap."
    $ chatter_list.append(vig2_epilogue_out_comment11)
    mS "Good. I'll be in my quarters."
    hide pilot_stream with dissolve
    hide engineer_stream with dissolve
    "I move to leave the briefing room when I feel a tug on my sleeve."
    $ chatter_list.append(vig2_epilogue_out_comment12)
    $ chatter_list.append(vig2_epilogue_out_comment13)
    show mac_stream at stream_center with Dissolve(0.5)
    macS "May I ask a question?"
    mS "Of course MAC."
    macS "I do not know how to feel about our mission. Why did we need to help the skeeve Matticus?"
    $ chatter_list.append(vig2_epilogue_out_comment14)
    menu:
        "Doing bad things now can let us do good things later":
            mS "Sometimes we have to do bad things so that we can do better things later."
            macS "But our conduct hurt innocent people. Does that not make us skeeves?"
            $ chatter_list.append(vig2_epilogue_out_comment15)
            mS "It's more complicated than that. Sometimes skeeves do good things, and sometimes non-skeeves do bad things."
            mS "There's no one-size-fits-all rule."
            $ chatter_list.append(vig2_epilogue_out_comment16)
            mS "If we didn't help Matticus, we might've gotten caught. And then a lot more innocent people would be hurt."
            macS "I see. The universe is confusing."
            $ chatter_list.append(vig2_epilogue_out_comment17)
            mS "Agreed."
        "The universe is bad and we can't trust anyone":
            mS "The universe is a dangerous place. We can never know who's looking to stab us in the back."
            mS "Better to hide than risk that I'd say."
            $ chatter_list.append(vig2_epilogue_out_comment15)
            macS "But we broke the law many times. How can we be good if we contribute to the danger of the universe?"
            mS "The universe was dangerous before us and it'll be the same after we're gone. Following the rules won't change that."
            $ chatter_list.append(vig2_epilogue_out_comment16)
            mS "To change bad things, sometimes we have to do dangerous things."
            macS "I see. The universe is confusing."
            $ chatter_list.append(vig2_epilogue_out_comment17)
            mS "Agreed."
    $ narrator = reg_narrator
    $ macroNarration = True
    $ chatter_list.append(vig2_epilogue_out_comment18)
    hide mac_stream with dissolve
    player "Wow..."
    player "Guess that's the end of the chapter, seems like a good spot to end."
    $ chatter_list.append(vig2_epilogue_out_comment19)
    player "Alright everyone, thank you all for coming out! See ya next time!"
    hide bridge_stream with dissolve
    "You quit out of the game and watch as the people in the stream discuss the game."
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment20)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment21)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment22)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment23)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment24)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment25)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment26)
    pause 0.5
    $ chatter_list.append(vig2_epilogue_out_comment27)
    $ outlaw += 10
    "As the chat winds down, you take off your headset, and sign off of Twitch."
    hide screen streamChat
    hide screen streamDetails
    scene black with dissolve
    jump vig2_macro_start


#### MACRO GAME #######
label vig2_macro_start():
    $ narrator = reg_narrator
    $ macroNarration = True
    $ macroChoice = True
    $ menu = nvl_menu
    "You lean back in your chair and let your body relax now that you're no longer on camera."
    "Then you get a discord notification message."
    "It's from Jessie, your mod."
    scene discord with dissolve
    mod_nvl "Hey bud, great stream again, so glad you decided to keep going with the game!"
    mod_nvl "How're you holding up?"
    if outlaw >= marshal:
        mod_nvl "Seemed like the chat got really into the Outlaw route"
        jump vig2_macro_mod_outlaw1
    else:
        mod_nvl "Seemed like you had to manage the chat a bit this time around"
        jump vig2_macro_mod_marshal1

###Labels for the Outlaw playthrough###
label vig2_macro_mod_outlaw1():
    menu:
        "The stream is fun!":
            $ enthusiasm += 1
            player_nvl "Yeah, the stream feels really good!"
            jump vig2_macro_mod_outlaw2_enthusiastic
        "Not feeling great":
            $ reluctance += 1
            player_nvl "Honestly, I'm not feelin so great about it."
            jump vig2_macro_mod_outlaw2_reluctant
        "Kind of mixed":
            player_nvl "I'm ok? Idk, it's complicated."
            jump vig2_macro_mod_outlaw2_reluctant

label vig2_macro_mod_outlaw2_enthusiastic():
    mod_nvl "Hell yeah!"
    mod_nvl "How does it feel being the bad guy this time ;)"
    #$ menu = adv_menu
    menu:
        "It's fun!":
            $ enthusiasm += 2
            player_nvl "Surprisingly really fun!"
            jump vig2_macro_mod_outlaw3_enthusiastic
        "Hard but enjoyable":
            $ enthusiasm += 1
            player_nvl "It's a little tough cause it's not my usual style, but it's cool!"
            jump vig2_macro_mod_outlaw3_enthusiastic
        "Odd...":
            $ reluctance += 1
            player_nvl "That part's feelin kinda weird, I'm thinking of switching things up honestly"
            jump vig2_macro_mod_outlaw3_reluctant

label vig2_macro_mod_outlaw2_reluctant():
    mod_nvl "Oh? What's up? Talk to me."
    menu:
        "The choices are starting to weigh on me":
            player_nvl "I feel really good about the response from the stream, but the choices are starting to weigh on me a bit."
            jump vig2_macro_mod_outlaw3_reluctant
        "It's exhausting":
            player_nvl "It's really cool that I get to see more of the story, but it's real tiring to play."
            jump vig2_macro_mod_outlaw3_reluctant

label vig2_macro_mod_outlaw3_enthusiastic():
    mod_nvl "Great to hear!"
    mod_nvl "I thought I saw a sly smile when you chose to ally with Savlian haha"
    menu:
        "That got the chat going":
            $ curiosity = False
            player_nvl "omg the chant went {i}WILD{/i} at that part!"
            player_nvl "so fun!"
            jump vig2_macro_mod_outlawEnd
        "It was a cool side of the story":
            $ curiosity = True
            player_nvl "The different story paths are crazy, man!"
            player_nvl "I never realized how different they could feel."
            jump vig2_macro_mod_outlawEnd
        "I was so stressed at that part":
            $ reluctance += 1
            player_nvl "Really?"
            player_nvl "Honestly, I was so stressed out making that choice."
            mod_nvl "Huh, I couldn't tell"
            mod_nvl "Way to play it off!"
            jump vig2_macro_mod_outlawEnd

label vig2_macro_mod_outlaw3_reluctant():
    mod_nvl "For sure, that makes sense."
    mod_nvl "I could kind of tell you weren't really into doing Savlian's dirty work"
    menu:
        "It was what the stream wanted":
            $ curiosity = False
            player_nvl "That obvious?"
            player_nvl "Yeah, I guess I went that route cause it seemed like what the stream wanted."
            jump vig2_macro_mod_outlaw4_reluctant
        "I was trying to role play":
            $ curiosity = False
            player_nvl "Tbh, I had to dissassociate from that one a bit."
            player_nvl "I thought of Moze as 'Outlaw Moze.'"
            player_nvl "So I wasn't really doing what like I would do in that position."
            menu:
                "It was cool":
                    player_nvl "It was cool actually"
                    jump vig2_macro_mod_outlaw4_reluctant
                "It felt a little weird":
                    player_nvl "Kind of a weird 'out of body' sensation."
                    jump vig2_macro_mod_outlaw4_reluctant
        "I liked seeing another side of the story":
            $ curiosity = True
            player_nvl "It didn't feel great"
            player_nvl "But it was interesting to see a different spin on the story."
            player_nvl "Never really seen that side of Moze before"
            jump vig2_macro_mod_outlaw4_reluctant
        "That didn't really bother me":
            $ enthusiasm += 1
            player_nvl "Really?"
            player_nvl "That felt fine actually"
            jump vig2_macro_mod_outlaw4_reluctant

label vig2_macro_mod_outlaw4_reluctant():
    mod_nvl "So you thinking of changing it up?"
    menu:
        "Yeah, Marshal feels more right":
            $ reluctance += 2
            player_nvl "Yeah I am."
            player_nvl "Marshal just feels more fun for me"
            jump vig2_macro_mod_outlawEnd
        "Maybe":
            $ reluctance += 1
            player_nvl "It's not a big deal, really."
            player_nvl "But it does take a lot out of me. So maybe I'll pivot."
            jump vig2_macro_mod_outlawEnd
        "No, this is a good path to Affiliate":
            $ enthusiasm += 1
            player_nvl "No, I think this is still the best way to get Affiliate."
            player_nvl "It's keeping me going tbh"
            jump vig2_macro_mod_outlawEnd

label vig2_macro_mod_outlawEnd():
    mod_nvl "Well you have this momentum now, and the audience does seem to like when you play Outlaw"
    mod_nvl "But it's your stream y'know"
    mod_nvl "If you're feeling good, then keep it going!"
    mod_nvl "If not then don't"
    mod_nvl "I got your back, whatever you choose"
    menu:
        "Thanks":
            player_nvl "Thanks b, I appreciate it"
    mod_nvl "Np."
    mod_nvl "Gotta run, gonna go grab some pho with a friend, chat later ya?"
    player_nvl "For sure, enjoy!"
    jump vig2_macro_bro1


###Labels for Marshal playthrough###
label vig2_macro_mod_marshal1():
    menu:
        "Stream felt good":
            $ reluctance += 1
            player_nvl "Yeah it was a bit hectic"
            player_nvl "But I really like where the stream is at!"
            jump vig2_macro_mod_marshal2_reluctant
        "A bit confused":
            $ enthusiasm += 1
            player_nvl "Honestly, I feel kinda confused"
            jump vig2_macro_mod_marshal2_enthusiastic
        "Complicated":
            player_nvl "I'm ok?"
            player_nvl "Idk, it's complicated."
            jump vig2_macro_mod_marshal2_enthusiastic

label vig2_macro_mod_marshal2_reluctant():
    mod_nvl "Hell yeah!"
    mod_nvl "Going back to Marshal felt like the right call then?"
    menu:
        "Definitely, it felt good":
            $ reluctance += 2
            player_nvl "Definitely, it just feels more right to me"
            jump vig2_macro_mod_marshal3_reluctant
        "Pretty much, chat seemed nicer":
            $ reluctance += 1
            player_nvl "Yeah, I think the vibes in chat are better for it"
            jump vig2_macro_mod_marshal3_reluctant
        "I'm not so sure, actually":
            $ enthusiasm += 1
            player_nvl "Actually, I'm not so sure." 
            player_nvl "I kinda miss the excitement ofthe Outlaw style"
            jump vig2_macro_mod_marshal3_enthusiastic

label vig2_macro_mod_marshal2_enthusiastic():
    mod_nvl "Oh? What's up? Talk to me."
    menu:
        "Stream doesn't seem as entertained":
            player_nvl "It's like..."
            player_nvl "I definitely prefer player Marshal. But I feel like that's not what the stream wants to see."
            jump vig2_macro_mod_marshal3_enthusiastic
        "The story felt predictable":
            player_nvl "I don't know."
            player_nvl "The story felt a little predictable. Like I'd seen it before."
            jump vig2_macro_mod_marshal3_enthusiastic

label vig2_macro_mod_marshal3_enthusiastic():
    mod_nvl "No that makes sense"
    mod_nvl "You did seem to take longer than usual to ally with that humanitarian group"
    menu:
        "I second-guessed myself a bit":
            player_nvl "That obvious?"
            player_nvl "I guess I kind of second-guessed myself there."
            jump vig2_macro_mod_marshal4_enthusiastic
        "Had to check in with chat":
            $ curiosity = False
            player_nvl "Yea, I needed some time to take stock of the chat"
            jump vig2_macro_mod_marshal4_enthusiastic
        "I was considering the Outlaw choice":
            $ curiosity = True
            player_nvl "I was open to going Outlaw, but I liked the arguments for Marshal better."
            jump vig2_macro_mod_marshal4_enthusiastic
        "I didn't notice":
            player_nvl "Really? That felt fine actually."
            jump vig2_macro_mod_marshal4_enthusiastic

label vig2_macro_mod_marshal3_reluctant():
    mod_nvl "Great to hear!"
    mod_nvl "You seemed pretty stoked to betray Matticus"
    menu:
        "I felt good energy from chat with that":
            $ curiosity = False
            player_nvl "Honestly, that was mostly talking through the choice with chat."
            jump vig2_macro_mod_marshalEnd
        "Definitely, I wonder about the Outlaw choice":
            $ curiosity = True
            player_nvl "100\% the right move."
            player_nvl "I do wonder what would've happened if I went Outlaw there tho."
            jump vig2_macro_mod_marshalEnd
        "I was kind of stressed":
            player_nvl "Really?"
            player_nvl "Honestly, I was so stressed out making that choice."
            mod_nvl "Huh, I couldn't tell."
            mod_nvl "Way to play it off!"
            jump vig2_macro_mod_marshalEnd

label vig2_macro_mod_marshal4_enthusiastic():
    mod_nvl "So you thinking of changing it up?"
    menu:
        "Yeah, Outlaw's good for viewers":
            $ enthusiasm += 1
            player_nvl "Yeah, it kinda feels like I should try Outlaw again."
            player_nvl "May be the best shot to get Affiliate?"
            jump vig2_macro_mod_marshalEnd
        "Maybe":
            $ reluctance += 1
            player_nvl "Maybe?"
            player_nvl "I'm still not sure."
            jump vig2_macro_mod_marshalEnd
        "No, Marshal feels right":
            $ reluctance += 2
            player_nvl "No. Even if Outlaw gets more views, this still feels right to me."
            jump vig2_macro_mod_marshalEnd

label vig2_macro_mod_marshalEnd():
    mod_nvl "Well the audience does seem to like when you play Outlaw, but there's still some Marshal fans out there too"
    mod_nvl "It's your stream tho"
    mod_nvl "If you're feeling good, then keep it going!"
    mod_nvl "If not then change it up!"
    mod_nvl "I got your back, whatever you choose"            
    menu:
        "Thanks":
            player_nvl "Thanks b, I appreciate it"
    mod_nvl "Np."
    mod_nvl "Gotta run, gonna go grab some fried rice with a friend"
    mod_nvl "chat later, ya?"
    player_nvl "For sure, enjoy!"
    jump vig2_macro_bro1

label vig2_macro_bro1():
    hide discord with dissolve
    scene black with dissolve
    nvl clear
    "You shut down the computer and start winding down for the evening."
    "You have a quick dinner and then finish up homework for class the next day."
    "As you brush your teeth, you see a notification on your phone that you missed while reading."
    scene discord with dissolve
    "It's from Elliot, a couple hours ago."
    bro_nvl "Hey bro, just wanted to let you know I caught the first stream VOD for Oakley 2."
    bro_nvl "Crazy stuff! I really like the direction they're taking Moze's character."
    bro_nvl "and OMG MAC"
    bro_nvl "SO CUTE!"
    bro_nvl "I love him. Would die for him. we stan 1000\%"
    bro_nvl "Also..."
    bro_nvl "Not you going \"bad guy\" Moze this time around?"
    bro_nvl "You've changed bro. Like a couple years at college and now you're a big bad Outlaw, crazyyyy"
    bro_nvl "Jkjk, hope you're having fun, excited to follow the stream!"
    "Based on the time change, Elliot's probably already asleep. But you decide to shoot him a message anyway." #check if this messes with the NVL sequence
    menu:
        "Thanks!":
            player_nvl "Thanks El, really appreciate you checking it out!"
        "Outlaw life is good!":
            $ enthusiasm += 1
            player_nvl "Felt like time for a change of pace"
            player_nvl "Outlaw life is pretty sweet!"
        "That was an accident":
            player_nvl "That was an accident lol"
            player_nvl "I had to play it off on stream :'("
            menu:
                "The chat's been a lot of fun":
                    $ enthusiasm += 1
                    player_nvl "But it's been real fun to play around with chat."
                    player_nvl "Lotta bits in the chat, you'll see next episode!"
                "The characters are really cool":
                    $ enthusiasm += 1
                    player_nvl "But the character dynamics are reallying interesting this route"
                    player_nvl "It's a compelling spin on Moze!"
                "Debating going back to Marshal, miss playing with you":
                    $ reluctance += 2
                    player_nvl "I'm debating going back to Marshal style."
                    player_nvl "Game doesnt feel as rewarding as when we were playing together"
                "Debating going back to Marshal, will see how story develops":
                    $ reluctance += 1
                    player_nvl "Could go back to Marshal, idk"
                    player_nvl "Will have to see how the story goes"
    player_nvl "Hope things with Jackson are going good! Love you <3"
    "You send the texts and then finish getting ready for bed."
    jump vig2_macro_sleep

label vig2_macro_sleep():
    scene black with dissolve
    $ menu = adv_menu
    "It's been a long day and you feel the tension dissipate from your muscles as you get into bed."
    "You lay your head on the pillow."
    if reluctance > enthusiasm and outlaw > marshal:
        "It's been a long day and the stream took a lot out of you."
        "Your mind is foggy, a cloud of robots, outlaws, and spaceships."
        "What do you think about as you drift to sleep?"
    if reluctance > enthusiasm and marshal >= outlaw:
        "There's still some leftover adrenaline from the stream."
        "Your mind leaps through a series of images, robots, outlaws, and spaceships."
        "What do you think about as you lay awake in bed?"
    if enthusiasm > reluctance and outlaw > marshal:
        "There's still some leftover adrenaline from the stream."
        "Your mind leaps through a series of images, robots, outlaws, and spaceships."
        "What do you think about as you lay awake in bed?"
    if enthusiasm > reluctance and marshal > outlaw:
        "It's been a long day and the stream took a lot out of you."
        "Your mind is foggy, a cloud of robots, outlaws, and spaceships."
        "What do you think about as you drift to sleep?"
    menu:
        "What do you think about?"
        "Think about your conversations with chat":
            "So many people watch you play a game."
            "It's fun...right?"
            menu:
                "Yeah!":
                    $ energy += 1
                    "Yeah, it is fun!"
                "Yes":
                    "It is."
                "Sometimes":
                    "It is. Most of the time."
                "Not always":
                    $ energy -= 1
                    "A lot of the time it just feels draining."
        "Think about how tired you are":
            "Sleep is going to feel so good"
            $ energy += 1
        "Think about getting to Affiliate":
            "It's not going to be easy, but you know you can do this."
            "In your mind you picture it: a glossier stream set up, clear-sounding microphones, and 100+ viewers in chat."
    
    "As you start to drift to sleep, you can't stop your mind from wandering back to the last episode of Oakley: Settle the Score."
    if outlaw > marshal:
        "You picture the comms base and the characters inside and feel..."
        menu:
            "Regret":
                $ reluctance += 1
                "Even though it's just a game, you feel like you made the wrong choice."
            "Regret?":
                "A mixture of both regret and excitement."
                "Even though it's just a game, you feel confused."
            "Confidence":
                $ enthusiasm += 1
                "You had to do what you had to do."
                "You know this."
            "A desire to replay the game":
                $ energy += 1
                "You imagine how the end game scenario would have played out if you chose differently, and you feel excitement."
                "You can always play the game again, after all."
            "Nothing":
                "You don't feel anything in particular."
            "Pressure from chat":
                $ reluctance += 1
                "You remember the comments from chat, like small weights on a scale."
    else:
        "You picture MAC and feel..."
        menu:
                "Regret":
                    $ enthusiasm += 1
                    "Even though it's just a game, you feel like you made the wrong choice."
                    "What if MAC is more in danger now?"
                "Regret?":
                    "A mixture of both regret and excitement."
                    "Even though it's just a game, you feel confused."
                "A desire to replay the game":
                    $ energy += 1
                    "You imagine how the end game scenario would have played out if you chose differently, and you feel excitement."
                    "You can always play the game again, after all."
                "Confidence":
                    $ reluctance += 1
                    "There was no other choice. You couldn't ally with Matticus."
                "Nothing":
                    "You don't really feel anything in particular."
                "Pressure from chat":
                    $ reluctance += 1
                    "You remember the comments from Chat, like small weights on a scale."
                "Even more tired":
                    "Just thinking about the game makes your eyelids feel heavier."
        jump vig2_macro_end

label vig2_macro_end():
    "As your eyes finally start to become heavy, you hear a slight buzz from the nightstand."
    "A discord notification. From Elliot."
    "You don't unlock the phone, because you can read the message from the lock screen."
    bro "Love you too!"
    return
